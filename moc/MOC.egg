-------------------------
-- MC and MOC grammars --
-------------------------

option auto = true;
option version = 0.0.1;
option k = 3;

inh source : MOCSourceFile for
         PROGRAM, ENTITIES, STRUCT_FIELDS, GLOBAL_DECL, INST, BLOC, INSTS, ELSE,
         FUNC_PARAMETERS, FUNC_PARAMETER, FUNC_PARAMETERSX, ARGS, ARGSX, AFF,
         E, EX, A, AX, AND, ANDX, BIT_OR, BIT_ORX, XOR, XORX, BIT_AND, BIT_ANDX,
         COMPEQ, COMPEQX, COMP, COMPX, SHIFT, SHIFTX, ADD, ADDX, MULT, MULTX,
         UNARY, DOTTED, DOTTEDX, F;
inh machine : IMachine for
         ENTITIES, STRUCT_FIELDS, GLOBAL_DECL, INST, BLOC, INSTS, ELSE,
         FUNC_PARAMETERS, FUNC_PARAMETER, FUNC_PARAMETERSX, ARGS, ARGSX, AFF,
         TYPE, STYPE, REFS,
         E, EX, A, AX, AND, ANDX, BIT_OR, BIT_ORX, XOR, XORX, BIT_AND, BIT_ANDX,
         COMPEQ, COMPEQX, COMP, COMPX, SHIFT, SHIFTX, ADD, ADDX, MULT, MULTX,
         UNARY, DOTTED, DOTTEDX, F;
inh st : ST for
         ENTITIES, STRUCT_FIELDS, GLOBAL_DECL, INST, BLOC, INSTS, ELSE,
         FUNC_PARAMETERS, FUNC_PARAMETER, FUNC_PARAMETERSX, ARGS, ARGSX, AFF,
         TYPE, STYPE,
         E, EX, A, AX, AND, ANDX, BIT_OR, BIT_ORX, XOR, XORX, BIT_AND, BIT_ANDX,
         COMPEQ, COMPEQX, COMP, COMPX, SHIFT, SHIFTX, ADD, ADDX, MULT, MULTX,
         UNARY, DOTTED, DOTTEDX, F;
inh types : LTYPES for FUNC_PARAMETERS, FUNC_PARAMETERSX, ARGS, ARGSX;
syn type : TTYPE for
         TYPE, STYPE, REFS, FUNC_PARAMETER,
         E, EX, A, AX, AND, ANDX, BIT_OR, BIT_ORX, XOR, XORX, BIT_AND, BIT_ANDX,
         COMPEQ, COMPEQX, COMP, COMPX, SHIFT, SHIFTX, ADD, ADDX, MULT, MULTX,
         UNARY, DOTTED, DOTTEDX, F;
inh htype : TTYPE for GLOBAL_DECL, REFS, AFF,
         EX, AX, ANDX, BIT_ORX, XORX, BIT_ANDX, COMPEQX, COMPX, SHIFTX, ADDX,
         MULTX, DOTTEDX;
inh hident : STRING for GLOBAL_DECL;
inh hinfo : INFOVAR for AFF;
syn name : STRING for COMPEQ_OP, COMP_OP, SHIFT_OP, ADD_OP, MULT_OP, UNARY_OP,
                      FUNC_PARAMETER;
inh plocator : ParametersLocator for FUNC_PARAMETERS, FUNC_PARAMETERSX;
inh vlocator : VariableLocator for BLOC, INSTS, INST, ELSE;

syn code : Code for
         INST, BLOC, INSTS, ELSE,
         ARGS, ARGSX, AFF,
         E, EX, A, AX, AND, ANDX, BIT_OR, BIT_ORX, XOR, XORX, BIT_AND, BIT_ANDX,
         COMPEQ, COMPEQX, COMP, COMPX, SHIFT, SHIFTX, ADD, ADDX, MULT, MULTX,
         UNARY, DOTTED, DOTTEDX, F;
syn globalCode : EntityCode for GLOBAL_DECL;
syn eCodes : EntityList for ENTITIES;
inh hcode : Code for
         EX, AX, ANDX, BIT_ORX, XORX, BIT_ANDX, COMPEQX, COMPX, SHIFTX, ADDX,
         MULTX, DOTTEDX, ARGSX;

syn exported : boolean for EXPORT;
inh hExported : boolean for GLOBAL_DECL;
inh hstruct : TSTRUCT for STRUCT_FIELDS;

-- to check subprogram call
inh function : TFUNCTION for ARGS, ARGSX;

space separator  is  "[\r\n\t ]+" ;
comment comm     is  "\/\/[^\n]*\n" ;
comment ml_comm  is  "\/\*([^\*]|(\*+[^\*\/]))*\*+\/" ;
sugar opar       is  "\(" ;
sugar cpar       is  "\)" ;
sugar ocbra      is  "\{" ;
sugar ccbra      is  "\}" ;
sugar comma      is  "," ;
sugar semicolon  is  "\;" ;
sugar affect     is  "=" ;
sugar export     is  "export" ;
sugar if         is  "if" ;
sugar else       is  "else" ;
sugar while      is  "while" ;
sugar t_for      is  "for" ;
sugar struct     is  "struct" ;
sugar void       is  "void" ;
sugar asm        is  "asm" ;
sugar int        is  "int" ;
sugar char       is  "char" ;
sugar bool       is  "bool" ;
sugar true       is  "true" ;
sugar false      is  "false" ;
sugar return     is  "return" ;
sugar null       is  "NULL" ;
sugar dot        is  "\." ;
sugar arrow      is  "\-\>" ;
sugar inf        is  "\<" ;
sugar infeq      is  "\<=" ;
sugar sup        is  "\>" ;
sugar supeq      is  "\>=" ;
sugar eq         is  "==" ;
sugar neq        is  "\!=" ;
sugar plus       is  "\+" ;
sugar minus      is  "\-" ;
sugar or         is  "\|\|" ;
sugar and        is  "\&\&" ;
sugar bit_or     is  "\|" ;
sugar bit_xor    is  "\^" ;
sugar bit_and    is  "\&" ;
sugar lshift     is  "\<\<" ;
sugar rshift     is  "\>\>" ;
sugar mult       is  "\*" ;
sugar div        is  "\/" ;
sugar mod        is  "\%" ;
sugar not        is  "\!" ;
sugar bit_not    is  "\~" ;
sugar colon      is  "\:" ;
sugar osbra      is  "\[" ;
sugar csbra      is  "\]" ;

term integer     is  "(0x[0-9a-fA-F]+)|(0b[01]+)|([0-9]+)" ;
term character   is  "\'([^\']|\\[0nrt])\'" ;
term string      is  "\"[^\"]*\"" ;
term ident       is  "[a-z][_0-9A-Za-z]*" ;


PROGRAM -> #init ENTITIES #gen ;
global
    IMachine machine;
    ST st;

#init {
    machine = PROGRAM^source.getMachine();
    ENTITIES^machine = machine;

    st = new ST();
    ENTITIES^st = st;
}

#gen {
    -- debug
    if (PROGRAM^source.getVerbosity() >= 1) {
        write("### final symbols table ###\n");
        write(st);
        write("###########################\n");
    }

    -- write in a file
    machine.writeCode(PROGRAM^source.getFileName(), ENTITIES^eCodes);
}

ENTITIES -> #gen ;
#gen {
    ENTITIES^eCodes = new EntityList();
}

ENTITIES -> asm opar string cpar ENTITIES #gen ;
#gen {
    ENTITIES1^eCodes.prepend(
        ENTITIES^machine.genGlobalAsm(string^txt, ENTITIES^st)
    );
    ENTITIES^eCodes = ENTITIES1^eCodes;
}

-- structure declaration
ENTITIES -> struct ident ocbra #trans STRUCT_FIELDS ccbra semicolon #st ENTITIES #gen ;
global TSTRUCT struct;

#trans {
    -- check if the variable already exists
    INFO i = ENTITIES^st.localSearch(ident^txt);

    if (i != null) {
        error(VAR_exists, ident^txt);
    }
    else {
        struct = new TSTRUCT();
        STRUCT_FIELDS^hstruct = struct;
    }
}

#st {
    ENTITIES^st.insert(ident^txt, new INFOSTRUCT(struct));
}

#gen {
    ENTITIES^eCodes = ENTITIES1^eCodes;
}

STRUCT_FIELDS -> ;

STRUCT_FIELDS -> TYPE ident semicolon #st STRUCT_FIELDS ;
#st {
    if(STRUCT_FIELDS^hstruct.hasField(ident^txt)) {
        error(FIELD_exists, ident^txt);
    }

    STRUCT_FIELDS^hstruct.addField(ident^txt, TYPE^type);
}

-- global declaration (variable, function)
ENTITIES -> EXPORT TYPE ident #trans GLOBAL_DECL ENTITIES #gen ;
#trans {
    -- check if the variable already exists
    INFO i = ENTITIES^st.localSearch(ident^txt);

    if (i != null) {
        error(VAR_exists, ident^txt);
    }
    else {
        GLOBAL_DECL^htype = TYPE^type;
        GLOBAL_DECL^hident = ident^txt;
        GLOBAL_DECL^hExported = EXPORT^exported;
    }
}

#gen {
    ENTITIES1^eCodes.prepend(GLOBAL_DECL^globalCode);
    ENTITIES^eCodes = ENTITIES1^eCodes;
}

EXPORT -> #gen ;
#gen {
    EXPORT^exported = false;
}

EXPORT -> export #gen ;
#gen {
    EXPORT^exported = true;
}

-- global vars
GLOBAL_DECL -> semicolon #gen ;
#gen {
    INFOVAR info = new INFOVAR(GLOBAL_DECL^htype, GLOBAL_DECL^machine.genGlobalLocation());
    GLOBAL_DECL^st.insert(GLOBAL_DECL^hident, info);
    GLOBAL_DECL^globalCode = GLOBAL_DECL^machine.genDeclGlobal(info);
}

-- functions
GLOBAL_DECL -> opar #trans FUNC_PARAMETERS cpar #st BLOC #gen ;
global LTYPES param_types;
    TFUNCTION fun;

#trans {
    -- symbols table for parameters
    ST st = new ST(GLOBAL_DECL^st);
    FUNC_PARAMETERS^st = st;
    BLOC^st = st;

    -- variable locator
    BLOC^vlocator = GLOBAL_DECL^machine.getVariableLocator();

    -- management of parameters
    param_types = new LTYPES();
    FUNC_PARAMETERS^types = param_types;
    FUNC_PARAMETERS^plocator = GLOBAL_DECL^machine.getParametersLocator();
}

#st {
    fun = new TFUNCTION(GLOBAL_DECL^hident, GLOBAL_DECL^htype, param_types);
    GLOBAL_DECL^st.insert(GLOBAL_DECL^hident, new INFOFUN(fun));
    GLOBAL_DECL^st.setCurrentFunction(fun);
}

#gen {
    GLOBAL_DECL^globalCode = GLOBAL_DECL^machine.genFunction(
        fun, BLOC^code, GLOBAL_DECL^hExported
    );
    GLOBAL_DECL^st.setCurrentFunction(null);
}

-- functions parameters
FUNC_PARAMETERS -> ;

FUNC_PARAMETERS -> FUNC_PARAMETER #trans FUNC_PARAMETERSX ;
#trans {
    STRING param_name = FUNC_PARAMETER^name;
    TTYPE param_type = FUNC_PARAMETER^type;

    FUNC_PARAMETERS^types.add(param_type);
    FUNC_PARAMETERSX^types = FUNC_PARAMETERS^types;

    -- useless, there is no other arguments for now
    INFO i = FUNC_PARAMETERS^st.localSearch(param_name);

    if (i != null) {
        error(PARAM_exists, param_name);
    }
    else {
        Location loc = FUNC_PARAMETERS^plocator.generate(param_type);
        INFOVAR info = new INFOVAR(param_type, loc);
        FUNC_PARAMETERS^st.insert(param_name, info);
    }
}

FUNC_PARAMETERSX -> ;
FUNC_PARAMETERSX -> comma FUNC_PARAMETER #trans FUNC_PARAMETERSX ;
#trans {
    STRING param_name = FUNC_PARAMETER^name;
    TTYPE param_type = FUNC_PARAMETER^type;

    FUNC_PARAMETERSX^types.add(param_type);
    FUNC_PARAMETERSX1^types = FUNC_PARAMETERSX^types;

    -- check if the parameter already exists
    INFO i = FUNC_PARAMETERSX^st.localSearch(param_name);

    if (i != null) {
        error(PARAM_exists, param_name);
    }
    else {
        Location loc = FUNC_PARAMETERSX^plocator.generate(param_type);
        INFOVAR info = new INFOVAR(param_type, loc);
        FUNC_PARAMETERSX^st.insert(param_name, info);
    }
}

FUNC_PARAMETER -> TYPE ident #type ;
#type {
    FUNC_PARAMETER^type = TYPE^type;
    FUNC_PARAMETER^name = ident^txt;
}

-- base types and pointers
TYPE -> STYPE #trans REFS #type ;
#trans {
    REFS^htype = STYPE^type;
}

#type {
    TYPE^type = REFS^type;
}

REFS -> #type ;
#type {
    REFS^type = REFS^htype;
}

REFS -> mult #trans REFS #type ;
#trans {
    -- type TPOINTER
    REFS1^htype = new TPOINTER(REFS^htype, REFS^machine.getPointerSize());
}

#type {
    REFS^type = REFS1^type;
}

-- arrays
REFS -> osbra integer csbra #trans REFS #type ;
#trans {
    REFS1^htype = new TARRAY(REFS^htype, REFS^machine.getIntFromString(integer^txt));
}

#type {
    REFS^type = REFS1^type;
}

REFS -> osbra csbra #trans REFS #type ;
#trans {
    REFS1^htype = new TPOINTER(REFS^htype, REFS^machine.getPointerSize());
}

#type {
    REFS^type = REFS1^type;
}

-- base types
STYPE -> void #type ;
#type {
    STYPE^type = new TVOID();
}

STYPE -> int #type ;
#type {
    STYPE^type = new TINTEGER(STYPE^machine.getIntSize());
}

STYPE -> char #type ;
#type {
    STYPE^type = new TCHAR(STYPE^machine.getCharSize());
}

STYPE -> bool #type ;
#type {
    STYPE^type = new TBOOL(STYPE^machine.getBoolSize());
}

STYPE -> struct ident #type ;
#type {
    INFO i = STYPE^st.globalSearch(ident^txt);

    if (i == null) {
        error(STRUCT_undefined, ident^txt);
    }
    else {
        match (i) {
            case INFOSTRUCT {
                INFOSTRUCT info = i;
                STYPE^type = info.getType();
            }
            default {
                error(STRUCT_undefined, ident^txt);
            }
        }
    }
}

-- instructions blocks
BLOC -> ocbra #trans INSTS ccbra #gen ;
global ST st;

#trans {
    st = new ST(BLOC^st);
    INSTS^st = st;
}

#gen {
    BLOC^code = BLOC^machine.genBlock(INSTS^code, BLOC^vlocator);

    -- debug
    if (BLOC^source.getVerbosity() >= 2) {
        write("### symbols table ###\n");
        write(st);
        write("#####################\n");
    }
}

-- instructions
INSTS -> #gen ;
#gen {
    INSTS^code = new Code("");
}

INSTS -> INST INSTS #gen ;
#gen {
    Code c = INST^code;
    c.appendAsm(INSTS1^code.getAsm());
    INSTS^code = c;
}

-- local variable declaration with or without initialization
INST -> TYPE ident #trans AFF semicolon #gen ;
#trans {
    AFF^htype = TYPE^type;

    -- check if the variable already exists
    INFO i = INST^st.localSearch(ident^txt);

    if (i != null) {
        error(VAR_exists, ident^txt);
    }
    else {
        INFOVAR info = new INFOVAR(TYPE^type, INST^vlocator.generate(TYPE^type));
        INST^st.insert(ident^txt, info);
        AFF^hinfo = info;
    }
}

#gen {
    INST^code = AFF^code;
}

AFF -> affect A #gen ;
#gen {
    if (!AFF^htype.constructFrom(A^type)) {
        error(TYPES_incompatible, AFF^htype, A^type);
    }

    AFF^code = AFF^machine.genDecl(AFF^hinfo, A^code, A^type);
}

AFF -> #gen ;
#gen {
    AFF^code = AFF^machine.genDecl(AFF^hinfo);
}

-- expression instruction (affectation and procedure call)
INST -> E semicolon #gen ;
#gen {
    INST^code = INST^machine.genInst(E^type, E^code);
}

-- instruction block
INST -> #trans BLOC #gen ;
#trans {
    BLOC^vlocator = INST^vlocator.getChild();
}

#gen {
    INST^code = BLOC^code;
}

-- conditional
INST -> if opar E cpar #trans BLOC ELSE #gen ;
#trans {
    if (!E^type.testable()) {
        error(TYPE_not_bool, E^type);
    }

    BLOC^vlocator = INST^vlocator.getChild();
    ELSE^vlocator = INST^vlocator.getChild();
}

#gen {
    INST^code = INST^machine.genConditional(E^code, BLOC^code, ELSE^code);
}

ELSE -> else BLOC #gen ;
#gen {
    ELSE^code = BLOC^code;
}

ELSE -> #gen ;
#gen {
    ELSE^code = new Code("");
}

-- while loop
INST -> while opar E cpar #trans BLOC #gen ;
#trans {
    if (!E^type.testable()) {
        error(TYPE_not_bool, E^type);
    }

    BLOC^vlocator = INST^vlocator.getChild();
}

#gen {
    INST^code = INST^machine.genWhileLoop(E^code, BLOC^code);
}

-- for loop
INST -> t_for opar E semicolon A semicolon E cpar #trans BLOC #gen ;
#trans {
    if (!A^type.testable()) {
        error(TYPE_not_bool, A^type);
    }
    if (!instanceof(E^type, TVOID)) {
        error(TYPE_not_assignment, E^type);
    }
    if (!instanceof(E1^type, TVOID)) {
        error(TYPE_not_assignment, E1^type);
    }

    BLOC^vlocator = INST^vlocator.getChild();
}

#gen {
    INST^code = INST^machine.genForLoop(E^code, A^code, E1^code, BLOC^code);
}

-- function return
INST -> return E semicolon #gen ;
#gen {
    TTYPE returnType;

    returnType = INST^st.getCurrentFunction().getReturnType();

    if (!returnType.constructFrom(E^type)) {
        error(TYPES_incompatible, returnType, E^type);
    }

    INST^code = INST^machine.genFunctionReturn(E^code, INST^st.getCurrentFunction());
}

-- inline asm
INST -> asm opar string cpar semicolon #gen ;
#gen {
    INST^code = INST^machine.genAsm(string^txt, INST^st);
}

-- expressions
-----------------------------------------------------------------------
-- E -> ( A = A ) | A
-- A -> AND ( || AND )*
-- AND -> BIT_OR ( && BIT_OR )*
-- BIT_OR -> XOR ( | BIT_OR )*
-- XOR -> BIT_AND ( ^ BIT_AND )*
-- BIT_AND -> COMPEQ ( & COMPEQ )*
-- COMPEQ -> COMP ( { == | != } COMP )*
-- COMP -> SHIFT ( { < | <= | > | >= } SHIFT )*
-- SHIFT -> ADD ( { << | >> } ADD)*
-- ADD -> MULT ( { + | - } MULT )*
-- MULT -> UNARY ( { * | / | % } UNARY )*
-- UNARY -> ( { + | - | ! | ~ | * | & | (TYPE) } UNARY ) | DOTTED
-- DOTTED -> F { .ident | ->ident }*
-- F -> integer | string | character | null | true | false | ident
--     | (E) | f(ARGS) | ident[A]
-----------------------------------------------------------------------

-- affectation
-- E -> ( A = A ) | A
E -> A #trans EX #gen ;
#trans {
    EX^htype = A^type;
    EX^hcode = A^code;
}

#gen {
    E^type = EX^type;
    E^code = EX^code;
}

EX -> affect A #gen ;
#gen {
    if (!EX^htype.constructFrom(A^type)) {
        error(TYPES_incompatible, EX^htype, A^type);
    }
    elseif (!EX^hcode.getIsAddress() && EX^hcode.getLocation() == null) {
        error(AFF_impossible);
    }

    EX^type = new TVOID();
    EX^code = EX^machine.genAffectation(EX^hcode, A^code, A^type);
}

EX -> #gen ;
#gen {
    EX^type = EX^htype;
    EX^code = EX^hcode;
}

-- or
-- A -> AND ( || AND )*
A -> AND #trans AX #gen ;
#trans {
    AX^htype = AND^type;
    AX^hcode = AND^code;
}

#gen {
    A^type = AX^type;
    A^code = AX^code;
}

AX -> or AND #trans AX #gen ;
#trans {
    if (!AX^htype.binaryUsable(AND^type, "||")) {
        error(TYPES_binary_unusable, AX^htype, AND^type, "||");
    }

    AX1^htype = new TBOOL(AX^machine.getBoolSize());
    AX1^hcode = AX^machine.genBinary(AX^hcode, AX^htype, AND^code, AND^type, "||");
}

#gen {
    AX^type = AX1^type;
    AX^code = AX1^code;
}

AX -> #gen ;
#gen {
    AX^type = AX^htype;
    AX^code = AX^hcode;
}

-- and
-- AND -> BIT_OR ( && BIT_OR )*
AND -> BIT_OR #trans ANDX #gen ;
#trans {
    ANDX^htype = BIT_OR^type;
    ANDX^hcode = BIT_OR^code;
}

#gen {
    AND^type = ANDX^type;
    AND^code = ANDX^code;
}

ANDX -> and BIT_OR #trans ANDX #gen ;
#trans {
    if (!ANDX^htype.binaryUsable(BIT_OR^type, "&&")) {
        error(TYPES_binary_unusable, ANDX^htype, BIT_OR^type, "&&");
    }

    ANDX1^htype = new TBOOL(ANDX^machine.getBoolSize());
    ANDX1^hcode = ANDX^machine.genBinary(ANDX^hcode, ANDX^htype, BIT_OR^code, BIT_OR^type, "&&");
}

#gen {
    ANDX^type = ANDX1^type;
    ANDX^code = ANDX1^code;
}

ANDX -> #gen ;
#gen {
    ANDX^type = ANDX^htype;
    ANDX^code = ANDX^hcode;
}

-- bit or
-- BIT_OR -> XOR ( | BIT_OR )*
BIT_OR -> XOR #trans BIT_ORX #gen ;
#trans {
    BIT_ORX^htype = XOR^type;
    BIT_ORX^hcode = XOR^code;
}

#gen {
    BIT_OR^type = BIT_ORX^type;
    BIT_OR^code = BIT_ORX^code;
}

BIT_ORX -> bit_or XOR #trans BIT_ORX #gen ;
#trans {
    if (!BIT_ORX^htype.binaryUsable(XOR^type, "|")) {
        error(TYPES_binary_unusable, BIT_ORX^htype, XOR^type, "|");
    }

    BIT_ORX1^htype = BIT_ORX^htype;
    BIT_ORX1^hcode = BIT_ORX^machine.genBinary(BIT_ORX^hcode, BIT_ORX^htype, XOR^code, XOR^type, "|");
}

#gen {
    BIT_ORX^type = BIT_ORX1^type;
    BIT_ORX^code = BIT_ORX1^code;
}

BIT_ORX -> #gen ;
#gen {
    BIT_ORX^type = BIT_ORX^htype;
    BIT_ORX^code = BIT_ORX^hcode;
}

-- xor
-- XOR -> BIT_AND ( ^ BIT_AND )*
XOR -> BIT_AND #trans XORX #gen ;
#trans {
    XORX^htype = BIT_AND^type;
    XORX^hcode = BIT_AND^code;
}

#gen {
    XOR^type = XORX^type;
    XOR^code = XORX^code;
}

XORX -> bit_xor BIT_AND #trans XORX #gen ;
#trans {
    if (!XORX^htype.binaryUsable(BIT_AND^type, "^")) {
        error(TYPES_binary_unusable, XORX^htype, BIT_AND^type, "^");
    }

    XORX1^htype = XORX^htype;
    XORX1^hcode = XORX^machine.genBinary(XORX^hcode, XORX^htype, BIT_AND^code, BIT_AND^type, "^");
}

#gen {
    XORX^type = XORX1^type;
    XORX^code = XORX1^code;
}

XORX -> #gen ;
#gen {
    XORX^type = XORX^htype;
    XORX^code = XORX^hcode;
}

-- bit and
-- BIT_AND -> COMPEQ ( & COMPEQ )*
BIT_AND -> COMPEQ #trans BIT_ANDX #gen ;
#trans {
    BIT_ANDX^htype = COMPEQ^type;
    BIT_ANDX^hcode = COMPEQ^code;
}

#gen {
    BIT_AND^type = BIT_ANDX^type;
    BIT_AND^code = BIT_ANDX^code;
}

BIT_ANDX -> bit_and COMPEQ #trans BIT_ANDX #gen ;
#trans {
    if (!BIT_ANDX^htype.binaryUsable(COMPEQ^type, "&")) {
        error(TYPES_binary_unusable, BIT_ANDX^htype, COMPEQ^type, "&");
    }

    BIT_ANDX1^htype = BIT_ANDX^htype;
    BIT_ANDX1^hcode = BIT_ANDX^machine.genBinary(BIT_ANDX^hcode, BIT_ANDX^htype, COMPEQ^code, COMPEQ^type, "&");
}

#gen {
    BIT_ANDX^type = BIT_ANDX1^type;
    BIT_ANDX^code = BIT_ANDX1^code;
}

BIT_ANDX -> #gen ;
#gen {
    BIT_ANDX^type = BIT_ANDX^htype;
    BIT_ANDX^code = BIT_ANDX^hcode;
}

-- comparision (=, !=)
-- COMPEQ -> COMP ( { == | != } COMP )*
COMPEQ -> COMP #trans COMPEQX #gen ;
#trans {
    COMPEQX^htype = COMP^type;
    COMPEQX^hcode = COMP^code;
}

#gen {
    COMPEQ^type = COMPEQX^type;
    COMPEQ^code = COMPEQX^code;
}

COMPEQX -> COMPEQ_OP COMP #trans COMPEQX #gen ;
#trans {
    if (!COMPEQX^htype.comparableWith(COMP^type, COMPEQ_OP^name)) {
        error(TYPES_incomparable, COMPEQX^htype, COMP^type, COMPEQ_OP^name);
    }

    COMPEQX1^htype = new TBOOL(COMPEQX^machine.getBoolSize());
    COMPEQX1^hcode = COMPEQX^machine.genBinary(COMPEQX^hcode, COMPEQX^htype, COMP^code, COMP^type, COMPEQ_OP^name);
}

#gen {
    COMPEQX^type = COMPEQX1^type;
    COMPEQX^code = COMPEQX1^code;
}

COMPEQX -> #gen ;
#gen {
    COMPEQX^type = COMPEQX^htype;
    COMPEQX^code = COMPEQX^hcode;
}

COMPEQ_OP -> eq #name ;
#name { COMPEQ_OP^name = "=="; }

COMPEQ_OP -> neq #name ;
#name { COMPEQ_OP^name = "!="; }

-- comparision (<, <=, >, >=)
-- COMP -> SHIFT ( { < | <= | > | >= } SHIFT )*
COMP -> SHIFT #trans COMPX #gen ;
#trans {
    COMPX^htype = SHIFT^type;
    COMPX^hcode = SHIFT^code;
}

#gen {
    COMP^type = COMPX^type;
    COMP^code = COMPX^code;
}

COMPX -> COMP_OP SHIFT #trans COMPX #gen ;
#trans {
    if (!COMPX^htype.comparableWith(SHIFT^type, COMP_OP^name)) {
        error(TYPES_incomparable, COMPX^htype, SHIFT^type, COMP_OP^name);
    }

    COMPX1^htype = new TBOOL(COMPX^machine.getBoolSize());
    COMPX1^hcode = COMPX^machine.genBinary(COMPX^hcode, COMPX^htype, SHIFT^code, SHIFT^type, COMP_OP^name);
}

#gen {
    COMPX^type = COMPX1^type;
    COMPX^code = COMPX1^code;
}

COMPX -> #gen ;
#gen {
    COMPX^type = COMPX^htype;
    COMPX^code = COMPX^hcode;
}

COMP_OP -> inf #name ;
#name { COMP_OP^name = "<"; }

COMP_OP -> infeq #name ;
#name { COMP_OP^name = "<="; }

COMP_OP -> sup #name ;
#name { COMP_OP^name = ">"; }

COMP_OP -> supeq #name ;
#name { COMP_OP^name = ">="; }

-- shift
-- SHIFT -> ADD ( { << | >> } ADD)*
SHIFT -> ADD #trans SHIFTX #gen ;
#trans {
    SHIFTX^htype = ADD^type;
    SHIFTX^hcode = ADD^code;
}

#gen {
    SHIFT^type = SHIFTX^type;
    SHIFT^code = SHIFTX^code;
}

SHIFTX -> SHIFT_OP ADD #trans SHIFTX #gen ;
#trans {
    if (!SHIFTX^htype.binaryUsable(ADD^type, SHIFT_OP^name)) {
        error(TYPES_binary_unusable, SHIFTX^htype, ADD^type, SHIFT_OP^name);
    }

    SHIFTX1^htype = SHIFTX^htype;
    SHIFTX1^hcode = SHIFTX^machine.genBinary(SHIFTX^hcode, SHIFTX^htype, ADD^code, ADD^type, SHIFT_OP^name);
}

#gen {
    SHIFTX^type = SHIFTX1^type;
    SHIFTX^code = SHIFTX1^code;
}

SHIFTX -> #gen ;
#gen {
    SHIFTX^type = SHIFTX^htype;
    SHIFTX^code = SHIFTX^hcode;
}

SHIFT_OP -> lshift #name ;
#name { SHIFT_OP^name = "<<"; }

SHIFT_OP -> rshift #name ;
#name { SHIFT_OP^name = ">>"; }

-- add
-- ADD -> MULT ( { + | - } MULT )*
ADD -> MULT #trans ADDX #gen ;
#trans {
    ADDX^htype = MULT^type;
    ADDX^hcode = MULT^code;
}

#gen {
    ADD^type = ADDX^type;
    ADD^code = ADDX^code;
}

ADDX -> ADD_OP MULT #trans ADDX #gen ;
#trans {
    if (!ADDX^htype.binaryUsable(MULT^type, ADD_OP^name)) {
        error(TYPES_binary_unusable, ADDX^htype, MULT^type, ADD_OP^name);
    }

    ADDX1^htype = ADDX^htype;
    ADDX1^hcode = ADDX^machine.genBinary(ADDX^hcode, ADDX^htype, MULT^code, MULT^type, ADD_OP^name);
}

#gen {
    ADDX^type = ADDX1^type;
    ADDX^code = ADDX1^code;
}

ADDX -> #gen ;
#gen {
    ADDX^type = ADDX^htype;
    ADDX^code = ADDX^hcode;
}

ADD_OP -> plus #name ;
#name { ADD_OP^name = "+"; }

ADD_OP -> minus #name ;
#name { ADD_OP^name = "-"; }

-- mult
-- MULT -> UNARY ( { * | / | % } UNARY )*
MULT -> UNARY #trans MULTX #gen ;
#trans {
    MULTX^htype = UNARY^type;
    MULTX^hcode = UNARY^code;
}

#gen {
    MULT^type = MULTX^type;
    MULT^code = MULTX^code;
}

MULTX -> MULT_OP UNARY #trans MULTX #gen ;
#trans {
    if (!MULTX^htype.binaryUsable(UNARY^type, MULT_OP^name)) {
        error(TYPES_binary_unusable, MULTX^htype, UNARY^type, MULT_OP^name);
    }

    MULTX1^htype = MULTX^htype;
    MULTX1^hcode = MULTX^machine.genBinary(MULTX^hcode, MULTX^htype, UNARY^code, UNARY^type, MULT_OP^name);
}

#gen {
    MULTX^type = MULTX1^type;
    MULTX^code = MULTX1^code;
}

MULTX -> #gen ;
#gen {
    MULTX^type = MULTX^htype;
    MULTX^code = MULTX^hcode;
}

MULT_OP -> mult #name ;
#name { MULT_OP^name = "*"; }

MULT_OP -> div #name ;
#name { MULT_OP^name = "/"; }

MULT_OP -> mod #name ;
#name { MULT_OP^name = "%"; } --" (fix vim syntax)

-- unary
-- UNARY -> ( { + | - | ! | ~ | * | & | (TYPE) } UNARY ) | DOTTED
UNARY -> UNARY_OP UNARY #gen ;
#gen {
    if (!UNARY1^type.unaryUsable(UNARY_OP^name)) {
        error(TYPE_unary_unusable, UNARY1^type, UNARY_OP^name);
    }

    UNARY^type = UNARY1^type;
    UNARY^code = UNARY^machine.genUnary(UNARY1^code, UNARY1^type, UNARY_OP^name);
}

-- unary expressions
UNARY_OP -> plus #name ;
#name { UNARY_OP^name = "+"; }

UNARY_OP -> minus #name ;
#name { UNARY_OP^name = "-"; }

UNARY_OP -> not #name ;
#name { UNARY_OP^name = "!"; }

UNARY_OP -> bit_not #name ;
#name { UNARY_OP^name = "~"; }

-- pointed-to memory access
UNARY -> mult UNARY #gen ;
#gen {
    match (UNARY1^type) {
        case TPOINTER {
            TPOINTER p = UNARY1^type;
            UNARY^type = p.getType();
            UNARY^code = UNARY^machine.genAccess(UNARY1^code, p.getType());
        }
        default {
            error(TYPE_not_pointer, UNARY1^type);
        }
    }
}

-- address
UNARY -> bit_and ident #gen ;
#gen {
    -- search for ident in the symbols table
    INFO i = UNARY^st.globalSearch(ident^txt);

    if (i == null) {
        error(VAR_undefined, ident^txt);
    }
    else {
        match (i) {
            case INFOVAR {
                INFOVAR info = i;
                UNARY^type = new TPOINTER(info.getType(), UNARY^machine.getPointerSize());
                UNARY^code = UNARY^machine.genAddress(info);
            }
            default {
                error(INFO_not_var, ident^txt);
            }
        }
    }
}

-- cast
UNARY -> opar TYPE cpar UNARY #gen ;
#gen {
    if (UNARY1^type.equals(TYPE^type)) {
        warning(TYPE_cast_useless, TYPE^type.toString(), TYPE^type.toString());
    }
    elseif (!UNARY1^type.isCastableTo(TYPE^type)) {
        error(TYPE_uncastable, UNARY1^type, TYPE^type);
    }

    UNARY^type = TYPE^type;
    UNARY^code = UNARY^machine.genCast(TYPE^type, UNARY1^type, UNARY1^code);
}

UNARY -> DOTTED #gen ;
#gen {
    UNARY^type = DOTTED^type;
    UNARY^code = DOTTED^code;
}

-- dotted expressions
-- DOTTED -> F { .ident | ->ident }*
DOTTED -> F #trans DOTTEDX #gen ;
#trans {
    DOTTEDX^htype = F^type;
    DOTTEDX^hcode = F^code;
}

#gen {
    DOTTED^type = DOTTEDX^type;
    DOTTED^code = DOTTEDX^code;
}

DOTTEDX -> dot ident #trans DOTTEDX #gen ;
#trans {
    match (DOTTEDX^htype) {
        case TSTRUCT {
            TSTRUCT type = DOTTEDX^htype;

            if(!type.hasField(ident^txt)) {
                error(STRUCT_no_field, type, ident^txt);
            }
            else {
                FIELD field = type.getField(ident^txt);
                DOTTEDX1^htype = field.getType();
                DOTTEDX1^hcode = DOTTEDX^machine.genFieldAccess(type, field, DOTTEDX^hcode);
            }
        }
        default {
            error(TYPE_not_struct, DOTTEDX^htype);
        }
    }
}

#gen {
    DOTTEDX^type = DOTTEDX1^type;
    DOTTEDX^code = DOTTEDX1^code;
}

DOTTEDX -> arrow ident #trans DOTTEDX #gen ;
#trans {
    match (DOTTEDX^htype) {
        case TPOINTER {
            TPOINTER pointerType = DOTTEDX^htype;
            TTYPE pointedType = pointerType.getType();

            match (pointedType) {
                case TSTRUCT {
                        TSTRUCT type = pointedType;

                        if(!type.hasField(ident^txt)) {
                            error(STRUCT_no_field, type, ident^txt);
                        }
                        else {
                            FIELD field = type.getField(ident^txt);
                            DOTTEDX1^htype = field.getType();
                            DOTTEDX1^hcode = DOTTEDX^machine.genPointerFieldAccess(type, field, DOTTEDX^hcode);
                        }
                }
                default {
                    error(TYPE_not_struct_pointer, DOTTEDX^htype);
                }
            }
        }
        default {
            error(TYPE_not_struct_pointer, DOTTEDX^htype);
        }
    }
}

#gen {
    DOTTEDX^type = DOTTEDX1^type;
    DOTTEDX^code = DOTTEDX1^code;
}

DOTTEDX -> #gen ;
#gen {
    DOTTEDX^type = DOTTEDX^htype;
    DOTTEDX^code = DOTTEDX^hcode;
}

-- leaf
-- F -> integer | string | character | null | true | false | ident
--     | (E) | f(ARGS) | ident[A]

-- base expressions
F -> integer #gen ;
#gen {
    F^type = new TINTEGER(F^machine.getIntSize());
    F^code = F^machine.genInt(integer^txt);
}

F -> string #gen ;
#gen {
    F^type = new TPOINTER(new TCHAR(F^machine.getCharSize()), F^machine.getPointerSize());
    F^code = F^machine.genString(string^txt);
}

F -> character #gen ;
#gen {
    F^type = new TCHAR(F^machine.getCharSize());
    F^code = F^machine.genChar(character^txt);
}

F -> null #gen ;
#gen {
    F^type = new TNULL(F^machine.getPointerSize());
    F^code = F^machine.genNull();
}

F -> true #gen ;
#gen {
    F^type = new TBOOL(F^machine.getBoolSize());
    F^code = F^machine.genBool(1);
}

F -> false #gen ;
#gen {
    F^type = new TBOOL(F^machine.getBoolSize());
    F^code = F^machine.genBool(0);
}

-- parenthesised expression
F -> opar E cpar #gen ;
#gen {
    F^type = E^type;
    F^code = E^code;
}

-- subprogram call
F -> ident opar #hgen ARGS cpar #gen ;
global LTYPES args_types; TFUNCTION fun;

#hgen {
    args_types = new LTYPES();
    ARGS^types = args_types;

    -- search for the function in the symbols table
    INFO i = F^st.globalSearch(ident^txt);

    if (i == null) {
        error(FUN_undefined, ident^txt);
    }
    else {
        match (i) {
            case INFOFUN {
                INFOFUN info = i;
                fun = info.getType();
                ARGS^function = fun;
                F^type = info.getType().getReturnType();
            }
            default {
                error(INFO_not_fun, ident^txt);
            }
        }
    }
}

#gen {
    F^code = F^machine.genFunctionCall(
        F^st.getCurrentFunction().getName(),
        fun, ARGS^code
    );
}

F -> ident #gen ;
#gen {
    -- search for ident in the symbols table
    INFO i = F^st.globalSearch(ident^txt);

    if (i == null) {
        error(VAR_undefined, ident^txt);
    }
    else {
        match (i) {
            case INFOVAR {
                INFOVAR info = i;
                F^type = info.getType();
                F^code = F^machine.genVariable(info);
            }
            default {
                error(INFO_not_var, ident^txt);
            }
        }
    }
}

-- array access
F -> ident osbra A csbra #gen ;
#gen {
    -- search for ident in the symbols table
    INFO i = F^st.globalSearch(ident^txt);

    if (i == null) {
        error(VAR_undefined, ident^txt);
    }
    else {
        match (i) {
            case INFOVAR {
                INFOVAR info = i;
                TTYPE varType = info.getType();

                match (A^type) {
                    case TINTEGER {
                        match (varType) {
                            case TARRAY {
                                TARRAY type = varType;
                                F^type = type.getElementsType();
                                F^code = F^machine.genStackArrayAccess(info, A^code);
                            }
                            case TPOINTER {
                                TPOINTER type = varType;
                                F^type = type.getType();
                                F^code = F^machine.genPointerArrayAccess(info, A^code);
                            }
                            default {
                                error(TYPE_not_array, varType);
                            }
                        }
                    }
                    default {
                        error(TYPE_not_integer, A^type);
                    }
                }
            }
            default {
                error(INFO_not_var, ident^txt);
            }
        }
    }
}

-- subprogram call arguments
ARGS -> #gen ;
#gen {
    -- check number of parameters
    if (ARGS^types.size() < ARGS^function.getParameterTypes().size()) {
        error(ARGS_too_few_arguments, ARGS^function.getName());
    }

    ARGS^code = new Code("");
}

ARGS -> E #trans ARGSX #gen;
#trans {
    ARGS^types.add(E^type);
    ARGSX^types = ARGS^types;
    ARGSX^hcode = ARGS^machine.genArg(E^code, E^type);

    -- check number of parameters
    if (ARGS^types.size() > ARGS^function.getParameterTypes().size()) {
        error(ARGS_too_many_arguments, ARGS^function.getName());
    }
    else {
        TTYPE needed_type = ARGS^function.getParameterTypes().get(ARGS^types.size() - 1);
        if (!needed_type.constructFrom(E^type)) {
            error(TYPES_incompatible, needed_type, E^type);
        }
    }
}

#gen {
    ARGS^code = ARGSX^code;
}

ARGSX -> #gen ;
#gen {
    if (ARGSX^types.size() < ARGSX^function.getParameterTypes().size()) {
        error(ARGS_too_few_arguments, ARGSX^function.getName());
    }

    ARGSX^code = ARGSX^hcode;
}

ARGSX -> comma E #trans ARGSX #gen ;
#trans {
    ARGSX^types.add(E^type);
    ARGSX1^types = ARGSX^types;

    -- check number of parameters
    if (ARGSX^types.size() > ARGSX^function.getParameterTypes().size()) {
        error(ARGS_too_many_arguments, ARGSX^function.getName());
    }
    else {
        TTYPE needed_type = ARGSX^function.getParameterTypes().get(ARGSX^types.size() - 1);
        if (!needed_type.constructFrom(E^type)) {
            error(TYPES_incompatible, needed_type, E^type);
        }
    }

    Code c = ARGSX^machine.genArg(E^code, E^type);
    c.appendAsm(ARGSX^hcode.getAsm());
    ARGSX1^hcode = c;
}

#gen {
    ARGSX^code = ARGSX1^code;
}
